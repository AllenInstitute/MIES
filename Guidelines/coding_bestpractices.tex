\documentclass{scrartcl}

% Compilation instructions and required third party tools:
% - lualatex from texlive 2014 or newer
% - pygments from http://pygments.org (2.0.x or newer)
% - (already included) minted package from https://github.com/gpoore/minted (v2.0-alpha3 or newer)
% - (already included) AnonymousPro font in OTF format from http://www.fontsquirrel.com/fonts/Anonymous-Pro

\usepackage{fontspec}
\setmonofont{AnonymousPro}

\usepackage[english]{babel}
\usepackage{minted}
\setminted{autogobble, tabsize=4, fontfamily=tt}
\setminted[igor]{style=igor}

\date{18.11.2014}
\newcommand{\version}{0.1}

\usepackage[%
pdfa=true,
pdfprintscaling=None,
plainpages=true,
pdfencoding=auto,
hidelinks,
pdfauthor={Thomas Braun, byte physics, thomas.braun@byte-physics.de}
]{hyperref}

\author{Thomas Braun <\url{thomas.braun@byte-physics.de}>}
\title{Best Practices for Igor Pro}
\publishers{Version: \version}
%
\begin{document}
%
\maketitle
%
\section{General hints}
\begin{itemize}
	\item Learn how to use wave assignment statements using \mintinline{igor}{p, q, r, s}
	\item Avoid GUI functions like \mintinline{igor}{ControlInfo} in performance critical code as they are comparatively slow
	\item Use builtins instead of homebrown algorithms, operations like \mintinline{igor}{MatrixOP} and \mintinline{igor}{Extract} do their job fast and good
	\item Keep the number of memory allocations low, the most common operations to look out for are \mintinline{igor}{Make}, \mintinline{igor}{Redimension} and \mintinline{igor}{Duplicate}
\end{itemize}
%
\section{Anti Patterns}
%
\subsection{if and do/while instead of for loops}
Bad:
\begin{minted}{igor}
if(i > 0)
	do
		// do stuff
	while(i < count)
endif	
\end{minted}
Good:
\begin{minted}{igor}
for(i = 0; i < count; i += 1)
	// do stuff
endfor
\end{minted}
Reasons:
\begin{itemize}
	\item Easier to understand
	\item Shorter
	\item Less indentation
\end{itemize}
%
\subsection{Inconsistent WAVE/DFREF/NVAR/SVAR usage}
Bad:
\begin{minted}{igor}
	WAVE/Z wv = mywave
	numRows = DimSize(wv, 0)
\end{minted}
Good:
\begin{minted}{igor}
	// either if the wave always exists, don't add /Z
	WAVE wv = mywave
	numRows = DimSize(wv, 0)
	
	// or, if it might not exist, handle that case
	WAVE/Z wv = mywave

	if(!WaveExists(wv))
		Abort "Missing wave"
	endif
	
	numRows = DimSize(wv, 0)
\end{minted}
Reasons:
\begin{itemize}
	\item Unclear intent
	\item Possibly incorrect
\end{itemize}
%
\subsection{Treating boolean variables as non-boolean}
Bad:
\begin{minted}{igor}
	// code which shows that var can be either 1 or 0
	if(var == 1)
		// code
	elseif(var == 0)
		// code
	endif
\end{minted}
Good:
\begin{minted}{igor}
	// In case var can be only 1 or 0, write it as
	if(var)
		// code 
	else
		// code 
	endif

	// Or handle the case where var != 1 and var != 0
	if(var == 1)
		// code 
	elseif(var == 0)
		// code 
	else
		// new code
	endif
\end{minted}
Reasons: 
\begin{itemize}
	\item Unclear intent
	\item Possibly incorrect
\end{itemize}
% 
\subsection{Superfluous comparison for boolean values}
Bad:
\begin{minted}{igor}
	if(WaveExists(wv) == 1)
		// code
	endif
\end{minted}
Good:
\begin{minted}{igor}
	if(WaveExists(wv))
		// code
	endif
\end{minted}
Reasons:
\begin{itemize}
  \item Less verbose code
\end{itemize}
% 
\subsection{Unnecessary variables}
Bad:
\begin{minted}{igor}
	variable var = GetVariable()
	SetVariable control value= _NUM:var
	// code which does not use var anymore
\end{minted}
\begin{minipage}{\textwidth}
Good:
\begin{minted}{igor}
	// Some reports indicate that this rewrite is not always possible.
	// Please report all such cases to your local Igor Pro guru
	// or to WaveMetrics directly
	SetVariable control value= _NUM:GetVariable()
\end{minted}
\end{minipage}\\[3ex]
Reasons:
\begin{itemize}
  \item Shorter code
  \item Less variables
\end{itemize}
% 
\subsection{Wave/Datafolder creator functions don't return the just created objects}
Bad:
\begin{minted}{igor}
	Function CreateWave(param)
		variable param

		Make myWave
	End

	Function someOtherFunction()
	
		WAVE/Z myWave

		if(!WaveExists(myWave))
			CreateWave(param)
		endif

		WAVE myWave
	End
\end{minted}
Good:
\begin{minted}{igor}
	Function/Wave GetWave(param)
		variable param

		// GetWaveFolder has the same logic as GetWave
		// Creates the datafolder if it does not exist
		// and returns a reference to it
		DFREF dfr = GetWaveFolder(param)

		WAVE/Z/SDFR=dfr data
		if(WaveExists(data))
			return data
		endif

		Make dfr:data/Wave=data
		// fill wave with default content

		return data
	End

	Function someOtherFunction()
		Wave wv = GetWave(param)
		
		// code
	End
\end{minted}
Reasons:
\begin{itemize}
  \item More reliable to use
  \item Less code at the call site
  \item Avoids code duplication at the call site
  \item Allows to handle changes to the wave structure in one place (centralized resource management)
\end{itemize}
% 
\subsection{Unnecessary use of StringMatch}
Bad:
\begin{minted}{igor}
	if(StringMatch(str, ""))
		// code
	endif
	
	if(StringMatch(str, "abcd"))
		// code
	endif
\end{minted}
Good:
\begin{minted}{igor}
	// can be written with a helper function if(isEmpty(str))
	if(!cmpstr(str, ""))
	    // ...
	endif

	// StringMatch is only required if you want to
	// use ! or * in the second parameter
	if(!cmpstr(str, "abcd"))
		// ...
	endif
\end{minted}
Reasons:
\begin{itemize}
	\item Faster
	\item Better readability
\end{itemize}
% 
\subsection{Unnecessary loops}
Bad:
\begin{minted}{igor}
	for(i = 10; i < 100; i += 1)
		mywave[i] = i^2
	endfor
\end{minted}
Good:
\begin{minted}{igor}
	mywave[10, 100] = p^2
\end{minted}
Reasons:
\begin{itemize}
	\item Much faster (at least ten times)
\end{itemize}
See also \mintinline{igor}{DisplayHelpTopic "Waveform Arithmetic and Assignment"} for an in-depth introduction to the topic.
%
\subsection{Unnecessary use of sprintf}
Bad:
\begin{minted}{igor}
	string str
	sprintf str, "%s" GetName()	
\end{minted}
Good:
\begin{minted}{igor}
	string str = GetName()
\end{minted}
Reasons:
\begin{itemize}
  \item Better readability
  \item Shorter
  \item Faster
\end{itemize}
% 
\subsection{Unnecessary use of Execute}
Bad:
\begin{minted}{igor}
	string cmd
	sprintf cmd, "wv = 1 + 2"
	Execute cmd
\end{minted}
Good:
\begin{minted}{igor}
	wv = 1 + 2
\end{minted}
\pagebreak
Reasons:
\begin{itemize}
  \item Better readability
  \item Shorter
  \item Faster
\end{itemize}
% 
Note: Unfortunately the ITC* operations are still old style operations, so for calling them using Execute is still mandatory. See also \\
\mintinline{igor}{DisplayHelpTopic "Calling an External Operation From a User-Defined Function"}.
% 
\subsection{Avoid relying on the top window and prefer structure based GUI control procedures}
Bad:
\begin{minted}{igor}
	Function ButtonProc(ctrlName) : ButtonControl
		String ctrlName

		GetWindow kwTopWin wtitle
		DoStuff(s_value)
	End
\end{minted}
Good:
\begin{minted}{igor}
	// In case execution of this ButtonControl takes a long time
	// and you want to prevent another call while the first is still
	// progressing have a look at WMButtonAction::blockreentry
	Function ButtonProc(ba) : ButtonControl
		struct WMButtonAction &ba

		switch(ba.eventCode)
			case 2:
				DoStuff(ba.win)
				break
		endswitch

		return 0
	End
\end{minted}
Reasons:
\begin{itemize}
  \item Less error prone (the top window could be different if ButtonProc is called programmatically)
  \item Easily expandable to other events
  \item Faster as only a reference to the structure must be passed to the function
\end{itemize}
% 
\subsection{Avoid magic numbers}
Bad:
\begin{minted}{igor}
	settings[11][] = height
\end{minted}
Good:
\begin{minted}{igor}
	// either with file level constants
	static Constant HEIGHT_ROW = 11

	Function DoStuff()
	
		settings[HEIGHT_ROW][] = height
	
	End

	// or dimension labels, use SetDimLabel on the wave before
	settings[%height][] = height

	// or a set function (rarely an appropriate choice)
	Function SetHeight(settings, height)
		WAVE settings
		variable height

		settings[11][] = height
	End

	SetHeight(settings, height)
\end{minted}
Reasons:
\begin{itemize}
	\item Better readability
	\item Less error prone
\end{itemize}
% 
\subsection{Unused parameters}
Bad:
\begin{minted}{igor}
	Function/S GetPath(param)
		string param

		return "root:myFolder" 
	End
\end{minted}
\pagebreak
Good:
\begin{minted}{igor}
	Function/S GetPath()
		return "root:myFolder" 
	End
\end{minted}
Reasons:
\begin{itemize}
  \item Better readability
  \item Does not fake a dependency of the function upon the parameter
\end{itemize}
Note: Unused function variables and file level constants should also be avoided. As there is currently no support from 
Igor Pro in doing that, visual inspection must be performed.
%
\subsection{Avoid function calls in loop statements}
Bad:
\begin{minted}{igor}
	for(i = 0; i < ItemsInList(list); i += 1)
		// code
	endfor
\end{minted}
Good:
\begin{minted}{igor}
	numItems = ItemsInList(list)
	for(i = 0; i < numItems ; i += 1)
		// code
	endfor
\end{minted}
Reasons:
\begin{itemize}
  \item Faster, the bad example calls \mintinline{igor}{ItemsInList} every time the loop condition is executed
\end{itemize}
%
\section{Performance tests}
%
\subsection{Dimension labels versus numerical indizes}
Using the following code
\begin{minted}{igor}
#pragma rtGlobals=3
#pragma igorVersion=6.3
#include <FunctionProfiling>

static Constant SIZE = 1e5

Function Prepare()

	variable i

	Make/O/N=(SIZE) data = p^2

	for(i = 0; i < SIZE; i += 1)
		SetDimLabel 0, i, $num2str(i), data
	endfor
End

Function Dimlabel()

	string str
	variable i, acc
	Wave data

	for(i = 0; i < SIZE; i += 1)
		str = num2str(i)
		acc += data[%$str]
	endfor

	print/D acc
	print str
End

Function NumericalIndex()

	string str
	variable i, acc
	Wave data

	for(i = 0; i < SIZE; i += 1)
		// The num2str call is included here in order to minimize
		// the number of differences compared to Dimlabel()
		str = num2str(i)
		acc += data[i]
	endfor

	print/D acc
	print str
End

Function PerformTest()

	Prepare()
	print "Array indexing with dimlabels"
	RunFuncWithProfiling(Dimlabel)

	print "Array indexing with numerical indizes"
	RunFuncWithProfiling(NumericalIndex)
End
\end{minted}
we can compare the speed of dimension labels and numerical indizes. This approach asssumes that the \mintinline{igor}{$} operator can be ignored in terms of execution speed.\\[2ex]
Calling \mintinline{igor}{PerformTest()}
\begin{minted}{text}
  Array indexing with dimlabels
  333328333526912
  99999
  Total time=   21.39
  Array indexing with numerical indizes
  333328333526912
  99999
  Total time=   0.110777
\end{minted}
clearly shows that numerical indizes are much faster than dimension labels.\\
Therefore you should use numeric indizes in performance critical code which usually is the case inside loops. In case you want to index a fixed element in a loop by dimension labels call \mintinline{igor}{FindDimLabel} before the loop and store the numerical index.
%
\end{document}
